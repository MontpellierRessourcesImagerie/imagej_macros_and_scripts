getDimensions(width, height, channels, slices, frames);
run("Properties...", "slices="+frames+" frames="+slices); 
setBatchMode("hide");
roiManager("reset");
run("Grays");
run("StackReg ", "transformation=[Rigid Body]");
title = getTitle();
run("Duplicate...", "duplicate");
titleSmoothed = getTitle();
run("Gaussian Blur...", "sigma=15 stack");
imageCalculator("Subtract create stack", title, titleSmoothed);
close(titleSmoothed);
run("Morphological Filters (3D)", "operation=Gradient element=Ball x-radius=2 y-radius=2 z-radius=2");
setAutoThreshold("Otsu dark stack");
Stack.setSlice(1);
run("Create Selection");
roiManager("add");
Stack.setSlice(nSlices);
run("Create Selection");
roiManager("add");
roiManager("Select", 0);
roiManager("Set Color", "red");
roiManager("Remove Slice Info");
roiManager("Select", 1);
roiManager("Set Color", "green");
roiManager("Remove Slice Info");
run("Select None");
close();
close();
roiManager("show all without labels");
run("Properties...", "slices="+slices+" frames="+frames);
setBatchMode("show");